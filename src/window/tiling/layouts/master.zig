//! Master-left layout

const std = @import("std");
const defs = @import("defs");
const log = @import("logging");
const utils = @import("utils");
const WM = defs.WM;

const tiling = @import("tiling");
const State = tiling.State;

pub fn tile(wm: *WM, state: *State, windows: []const u32, screen_w: u16, screen_h: u16) void {
    const n = windows.len;
    if (n == 0) return;

    const m = state.margins();
    const m_count: u16 = @intCast(@min(state.master_count, n));
    const s_count: u16 = @intCast(if (n > m_count) n - m_count else 0);

    // Calculate master area width
    const master_w: u16 = if (s_count > 0)
        @intFromFloat(@as(f32, @floatFromInt(screen_w)) * state.master_width_factor)
    else
        screen_w;

    log.debugMasterSide(state.master_side);

    // Determine if master is on right side
    const master_on_right = std.mem.eql(u8, state.master_side, "right");

    log.debugMasterPosition(master_on_right, if (master_on_right) screen_w - master_w else 0);

    const master_x: u16 = if (master_on_right) screen_w - master_w else 0;
    const stack_x: u16 = if (master_on_right) 0 else master_w;

    // Pre-calculate common values
    const margin_total = m.total();
    const master_inner_w = if (master_w > margin_total) master_w - margin_total else utils.MIN_WINDOW_DIM;

    // Tile master area
    const m_layout = utils.calcColumnLayout(screen_h, m_count, m);
    for (windows[0..m_count], 0..) |win, i| {
        const row: u16 = @intCast(i);
        utils.configureWindow(wm.conn, win, .{
            .x = @intCast(master_x + m.gap),
            .y = @intCast(m.gap + row * m_layout.spacing),
            .width = master_inner_w,
            .height = m_layout.item_h,
        });
    }

    if (s_count == 0) return;

    const stack_w = screen_w - master_w;
    const stack_windows = windows[m_count..];

    // Calculate how many windows fit at full height in the stack
    const space_per_window: u32 = utils.MIN_WINDOW_DIM + 2 * @as(u32, m.border) + @as(u32, m.gap);
    const available: u32 = @as(u32, screen_h) - @as(u32, m.gap);
    const max_fit: u16 = @intCast(@max(1, available / space_per_window));

    // Pre-calculate stack margin values
    const stack_margin = m.gap + 2 * m.border;

    if (s_count <= max_fit) {
        // All windows fit - single column at full width
        const s_layout = utils.calcColumnLayout(screen_h, s_count, m);
        const stack_inner_w = if (stack_w > stack_margin)
            @max(utils.MIN_WINDOW_DIM, stack_w - stack_margin)
        else
            utils.MIN_WINDOW_DIM;

        for (stack_windows, 0..) |win, i| {
            const row: u16 = @intCast(i);
            utils.configureWindow(wm.conn, win, .{
                .x = @intCast(stack_x + m.gap),
                .y = @intCast(m.gap + row * s_layout.spacing),
                .width = stack_inner_w,
                .height = s_layout.item_h,
            });
        }
    } else {
        // Overflow: progressively split rows as needed
        const s_layout = utils.calcColumnLayout(screen_h, max_fit, m);

        log.debugLayoutOverflow(max_fit);

        // Tile stack windows row by row
        var row: u16 = 0;
        while (row < max_fit) : (row += 1) {
            // Count how many windows are in this row
            var cols_in_row: u16 = 0;
            var win_idx = row;
            while (win_idx < s_count) : (win_idx += max_fit) {
                cols_in_row += 1;
            }

            if (cols_in_row == 0) break;

            const row_col_w = stack_w / cols_in_row;
            const y_pos = m.gap + row * s_layout.spacing;
            const row_inner_w = if (row_col_w > stack_margin)
                @max(utils.MIN_WINDOW_DIM, row_col_w - stack_margin)
            else
                utils.MIN_WINDOW_DIM;

            log.debugLayoutRow(row, cols_in_row);

            // Place all windows in this row
            var col: u16 = 0;
            win_idx = row;
            while (win_idx < s_count) : (win_idx += max_fit) {
                const win = stack_windows[win_idx];

                utils.configureWindow(wm.conn, win, .{
                    .x = @intCast(stack_x + col * row_col_w + m.gap),
                    .y = @intCast(y_pos),
                    .width = row_inner_w,
                    .height = s_layout.item_h,
                });

                log.debugLayoutWindowIndex(win_idx, row, col);

                col += 1;
            }
        }
    }
}
