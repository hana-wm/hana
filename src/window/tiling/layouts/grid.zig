// Grid layout - windows arranged in a grid pattern
const std = @import("std");
const defs = @import("defs");
const builtin = @import("builtin");
const log = @import("logging");
const WM = defs.WM;
const types = @import("types");
const TilingState = types.TilingState;

pub fn tile(wm: *WM, state: *TilingState, windows: []const u32, screen_w: u16, screen_h: u16) void {
    const n = windows.len;
    if (n == 0) return;

    const gap = state.gaps;
    const bw = state.border_width;

    const cols_f = @ceil(@sqrt(@as(f32, @floatFromInt(n))));
    const cols = @as(u16, @intFromFloat(cols_f));
    const rows = @as(u16, @intCast((n + cols - 1) / cols));

    if (builtin.mode == .Debug) {
        log.debugLayoutTiling("grid", n, cols, rows);
    }

    // Calculate cell size accounting for gaps: gap + cell + gap + cell + ... + gap
    // Total: (cols+1)*gap + cols*cell_w = screen_w
    // Therefore: cell_w = (screen_w - (cols+1)*gap) / cols
    const cell_w = (screen_w - (cols + 1) * gap) / cols;
    const cell_h = (screen_h - (rows + 1) * gap) / rows;

    // Window size is cell size minus borders
    const win_w = if (cell_w > 2 * bw) cell_w - 2 * bw else 1;
    const win_h = if (cell_h > 2 * bw) cell_h - 2 * bw else 1;

    for (windows, 0..) |win, idx| {
        const col = @as(u16, @intCast(idx % cols));
        const row = @as(u16, @intCast(idx / cols));

        // Position: gap + col * (cell + gap)
        const x = gap + col * (cell_w + gap);
        const y = gap + row * (cell_h + gap);

        types.configureWindow(wm, win, x, y, win_w, win_h);
    }
}
